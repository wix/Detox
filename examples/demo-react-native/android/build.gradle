buildscript {
    apply from: './rninfo.gradle'

    def androidGradlePluginVersion = rnInfo.isRN62OrHigher ? '3.5.3' : '3.4.2'
    println "[$project] Resorted to Android Gradle-plugin version $androidGradlePluginVersion"

    ext {
        kotlinVersion = '1.4.21'
        buildToolsVersion = '29.0.3'
        compileSdkVersion = 29
        targetSdkVersion = 29
        minSdkVersion = 18
    }

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath 'de.undercouch:gradle-download-task:4.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven {
            url "$rootDir/../node_modules/react-native/android"
        }
        maven {
            url "$rootDir/../node_modules/detox/Detox-android"
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        /*
         BINTRAY_SUNSET_HACK start
         (Section shouldn't be needed at all)
         */
        flatDir {
            dirs "$rootProject.projectDir/libs"
        }
        /*
         BINTRAY_SUNSET_HACK end
         */
    }
}

/*
 BINTRAY_SUNSET_HACK start
 (Shouldn't be here)
 */
subprojects {
    configurations.all {
        resolutionStrategy {
            force 'com.facebook.fbjni:fbjni-java-only:0.0.4'
        }
    }
}
/*
 BINTRAY_SUNSET_HACK end
 */
