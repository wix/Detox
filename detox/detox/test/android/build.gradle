buildscript {
    apply from: './rninfo.gradle'

    def androidGradlePluginVersion =
            rnInfo.isRN69OrHigher ? '7.1.1'
            : rnInfo.isRN68OrHigher ? '7.0.4'
            : rnInfo.isRN66OrHigher ? '4.2.2'
            : '4.1.0'

    println "[$project] Resorted to Android Gradle-plugin version $androidGradlePluginVersion"

    ext {
        isOfficialDetoxApp = true
        kotlinVersion = '1.6.21'
        buildToolsVersion = '31.0.0'
        compileSdkVersion = 31
        targetSdkVersion = 31
        minSdkVersion = 21
        appCompatVersion = (rnInfo.isRN68OrHigher ? '1.4.2' : '1.3.0')
    }
    ext.detoxKotlinVersion = ext.kotlinVersion

    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"
        classpath 'de.undercouch:gradle-download-task:5.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        // Needed by Spek (https://spekframework.org/setup-android)
        // Here in order to enable unit-tests run from Android Studio when working on the test app.
        classpath 'de.mannodermaus.gradle.plugins:android-junit5:1.7.1.1'
    }
}

allprojects {
    repositories {
        maven {
            url "$rootDir/../../node_modules/react-native/android"
        }
        maven {
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        google()
        mavenLocal()
    }
}

subprojects {
    afterEvaluate { p ->
        if (p.hasProperty('android')) {
            android {
                buildToolsVersion rootProject.ext.buildToolsVersion
            }
        }
    }
}
