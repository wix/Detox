buildscript {
    apply from: '../../android/rninfo.gradle'

    def androidGradlePluginVersion =
        rnInfo.isRN70OrHigher ? '7.3.1'
            : rnInfo.isRN69OrHigher ? '7.1.1'
            : '7.0.4'

    println "[$project] Resorted to Android Gradle-plugin version $androidGradlePluginVersion"

    ext {
        isOfficialDetoxApp = true
        kotlinVersion = '1.6.21'
        buildToolsVersion = '33.0.0'
        compileSdkVersion = 33
        targetSdkVersion = 33
        minSdkVersion = 21
        appCompatVersion = (rnInfo.isRN68OrHigher ? '1.4.2' : '1.3.0')
    }
    ext.detoxKotlinVersion = ext.kotlinVersion

    repositories {
        google()
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidGradlePluginVersion"

        // In RN .71, they've switched to the gradle plugin they've uploaded to maven-central
        if (rnInfo.isRN71OrHigher) {
            classpath 'com.facebook.react:react-native-gradle-plugin'
        }

        // Gradle task downloader seems to come built-in in newer versions of RN/Gradle
        if (!rnInfo.isRN71OrHigher) {
            classpath 'de.undercouch:gradle-download-task:5.0.1'
        }

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        // Needed by Spek (https://spekframework.org/setup-android)
        // Here in order to enable unit-tests run from Android Studio when working on the test app.
        classpath 'de.mannodermaus.gradle.plugins:android-junit5:1.7.1.1'
    }
}

allprojects {
    repositories {
        // In RN below 71, we want RN and hermes/js-core native code to come from node_modules/ rather
        // than from maven-central, because there are also older versions over there.
        if (rnInfo.isRN71OrHigher) {
            google()
            mavenCentral()
        } else {
            maven {
                url "$rootDir/../../node_modules/react-native/android"
            }
            maven {
                url("$rootDir/../node_modules/jsc-android/dist")
            }
            google()
            mavenCentral() {
                if (!rnInfo.isRN71OrHigher) {
                    content {
                        excludeGroup 'com.facebook.react'
                    }
                }
            }
        }

        mavenLocal()
    }
}

subprojects {
    afterEvaluate { p ->
        if (p.hasProperty('android')) {
            android {
                buildToolsVersion rootProject.ext.buildToolsVersion
            }
        }
    }
}
