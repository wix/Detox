apply plugin: 'com.android.application'
apply from: '../rninfo.gradle'

//project.ext.react = [:]
project.ext.react = [
    enableHermes: rnInfo.isRN60OrHigher
]
apply from: '../../node_modules/react-native/react.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'com.wix.detox.test'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName '1.0'
        ndk {
            abiFilters 'armeabi-v7a', 'x86', 'x86_64'
        }
        testBuildType System.getProperty('testBuildType', 'debug')
        testInstrumentationRunner 'com.example.DetoxTestAppJUnitRunner'

        /*
        testInstrumentationRunnerArguments = [
                'detoxServer': 'ws://localhost:8099',
                'detoxSessionId': 'test'
        ]
        */

        missingDimensionStrategy 'detox', 'full'
    }
    splits {
        abi {
            reset()
            enable false
            universalApk false  // If true, also generate a universal APK
            include 'armeabi-v7a', 'x86', 'x86_64'
        }
    }
    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword "12345678"
            keyAlias "key0"
            keyPassword "12345678"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', "${project(':detox').projectDir}/proguard-rules-app.pro"

            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        flavorDimensions 'compileRNFromSource'
        fromSource {
            dimension 'compileRNFromSource'
        }
        fromBin {
            dimension 'compileRNFromSource'
        }
    }
    packagingOptions {
        pickFirst '**/libc++_shared.so'

        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    fromSourceImplementation(project(path: ':ReactAndroid'))

    // noinspection GradleDynamicVersion
    fromBinImplementation('com.facebook.react:react-native:+')

    implementation project(':AsyncStorage')
    implementation project(':react-native-webview')
    implementation project(':react-native-community-checkbox')
    implementation project(':react-native-community-geolocation')

    androidTestImplementation(project(path: ':detox'))
    androidTestImplementation 'com.linkedin.testbutler:test-butler-library:2.2.1'
}

if (rnInfo.isRN60OrHigher) {
    // refs: https://react-native-community.github.io/upgrade-helper/?from=0.59.9&to=0.60.6
    //       https://react-native-community.github.io/upgrade-helper/?from=0.60.6&to=0.61.4
    dependencies {
        def enableHermes = project.ext.react.get('enableHermes', false)
        if (enableHermes) {
            def hermesModuleName = rnInfo.isRN61OrHigher ? 'hermes-engine' : 'hermesvm'
            def hermesPath = "../../node_modules/$hermesModuleName/android/"
            debugImplementation files(hermesPath + 'hermes-debug.aar')
            releaseImplementation files(hermesPath + 'hermes-release.aar')
        } else {
            implementation 'org.webkit:android-jsc:+'
        }
    }
}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
  from configurations.compile
  into 'libs'
}
