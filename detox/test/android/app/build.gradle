apply plugin: 'com.android.application'
apply from: '../../../android/rninfo.gradle'

if (rnInfo.isRN71OrHigher) {
    apply plugin: 'com.facebook.react'
} else {
    project.ext.react = [
        enableHermes: true
    ]
    apply from: '../../node_modules/react-native/react.gradle'
}

android {
    namespace 'com.example'

    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'com.wix.detox.test'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName '1.0'
        ndk {
            abiFilters 'armeabi-v7a', 'x86', 'x86_64', 'arm64-v8a'
        }
        testBuildType System.getProperty('testBuildType', 'debug')
        testInstrumentationRunner 'com.example.DetoxTestAppJUnitRunner'

        /*
        testInstrumentationRunnerArguments = [
                'detoxServer': 'ws://localhost:8099',
                'detoxSessionId': 'test'
        ]
        */

        missingDimensionStrategy 'detox', 'full'
    }
    splits {
        abi {
            reset()
            enable false
            universalApk false  // If true, also generate a universal APK
            include 'armeabi-v7a', 'x86', 'x86_64', 'arm64-v8a'
        }
    }
    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword "12345678"
            keyAlias "key0"
            keyPassword "12345678"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', "${project(':detox').projectDir}/proguard-rules-app.pro"

            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        flavorDimensions 'reactNativeVersion'
        rnDefault {
            dimension 'reactNativeVersion'
        }
        rnLegacy { // For RN < 71
            dimension 'reactNativeVersion'
        }
    }
    packagingOptions {
        pickFirst '**/libc++_shared.so'

        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    // The version of react-native is set by the React Native Gradle Plugin
    rnDefaultImplementation('com.facebook.react:react-android')
    // noinspection GradleDynamicVersion
    rnLegacyImplementation('com.facebook.react:react-native:+')

    implementation "androidx.appcompat:appcompat:${rootProject.ext.appCompatVersion}"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation project(path: ':@react-native-community_slider')
    implementation project(':AsyncStorage')
    implementation project(':react-native-webview')
    implementation project(':react-native-community-checkbox')
    implementation project(':react-native-community-geolocation')
    implementation project(':react-native-datetimepicker')
    implementation project(':react-native-launcharguments')

    androidTestImplementation(project(path: ':detox'))
    androidTestImplementation 'com.linkedin.testbutler:test-butler-library:2.2.1'
}

// Apply Hermes as our JS engine
dependencies {
    rnDefaultImplementation('com.facebook.react:hermes-android')
    // noinspection GradleDynamicVersion
    rnLegacyImplementation('com.facebook.react:hermes-engine:+') {
        exclude group: 'com.facebook.fbjni'
    }
}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
  from configurations.implementation
  into 'libs'
}
