buildscript {
    apply from: './rninfo.gradle'

    ext {
        isOfficialDetoxLib = true
        kotlinVersion = '1.6.21'
        dokkaVersion = '1.6.0'
        buildToolsVersion = '33.0.0'
        compileSdkVersion = 33
        targetSdkVersion = 33
        minSdkVersion = 21

        if (System.properties['os.arch'] == "aarch64") {
            // For M1 Users we need to use the NDK 24 which added support for aarch64
            ndkVersion = "24.0.8215888"
        } else {
            // Otherwise we default to the side-by-side NDK version from AGP.
            ndkVersion = "21.4.7075529"
        }
    }
    ext.detoxKotlinVersion = ext.kotlinVersion

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        if (!rnInfo.isRN71OrNewer) {
            classpath "com.facebook.react:react-native-gradle-plugin"
        }
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaVersion"

        // Needed by Spek (https://spekframework.org/setup-android)
        classpath 'de.mannodermaus.gradle.plugins:android-junit5:1.7.1.1'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()

        // In RN's below 71, the native code comes from within node_modules/ rather
        // than from maven-central.
        if (!rnInfo.isRN71OrNewer) {
            maven {
                url "$projectDir/../../node_modules/react-native/android"
            }
        }
    }
}

subprojects {
    afterEvaluate { p ->
        if (p.hasProperty('android')) {
            android {
                buildToolsVersion rootProject.ext.buildToolsVersion
            }
        }
    }
}
